
#-----------------------------------------------------------------------------
# Find Java JNI
#-----------------------------------------------------------------------------
find_package(JNI REQUIRED)
find_package(Java REQUIRED)
include(UseJava)
message("Found JNI at ${JNI_INCLUDE_DIRS} and ${JNI_LIBRARIES}")

#-----------------------------------------------------------------------------
# Build C++ Java wrapper
#-----------------------------------------------------------------------------
function(java_wrapper name path)
    include_directories(${JNI_INCLUDE_DIRS})
    add_library(${name} SHARED ${path})
    add_dependencies(${name} hermes)
    target_link_libraries(${name}
            hermes
            MPI::MPI_CXX stdc++fs dl ${JNI_LIBRARIES})
endfunction()
java_wrapper(hermes_java_Blob src/hermes/cpp/src_main_Blob.cc)
java_wrapper(hermes_java_Bucket src/hermes/cpp/src_main_Bucket.cc)
java_wrapper(hermes_java_Hermes src/hermes/cpp/src_main_Hermes.cc)

#-----------------------------------------------------------------------------
# Build Java API
#-----------------------------------------------------------------------------
add_custom_command(TARGET hermes_java_Blob POST_BUILD
        COMMAND cd ${CMAKE_CURRENT_SOURCE_DIR} &&
                ${CMAKE_CURRENT_SOURCE_DIR}/gradlew build -x test)
add_custom_target(build_hermes_java
        COMMAND cd ${CMAKE_CURRENT_SOURCE_DIR} &&
                ${CMAKE_CURRENT_SOURCE_DIR}/gradlew build -x test)
pytest(java_wrapper test_hermes_java)

#-----------------------------------------------------------------------------
# Add Target(s) to CMake Install
#-----------------------------------------------------------------------------
install(
        TARGETS
        hermes_java_Blob
        hermes_java_Bucket
        hermes_java_Hermes
        EXPORT
        ${HERMES_EXPORTED_TARGETS}
        LIBRARY DESTINATION ${HERMES_INSTALL_LIB_DIR}
        ARCHIVE DESTINATION ${HERMES_INSTALL_LIB_DIR}
        RUNTIME DESTINATION ${HERMES_INSTALL_BIN_DIR}
)
#-----------------------------------------------------------------------------
# Export all exported targets to the build tree for use by parent project
#-----------------------------------------------------------------------------
set(HERMES_EXPORTED_LIBS
        hermes_java_Blob
        hermes_java_Bucket
        hermes_java_Hermes
        ${HERMES_EXPORTED_LIBS})
if(NOT HERMES_EXTERNALLY_CONFIGURED)
    EXPORT (
            TARGETS
            ${HERMES_EXPORTED_LIBS}
            FILE
            ${HERMES_EXPORTED_TARGETS}.cmake
    )
endif()
#-----------------------------------------------------------------------------
# Add Target(s) to Coverage
#-----------------------------------------------------------------------------
if(HERMES_ENABLE_COVERAGE)
#    set_coverage_flags(hermes_src_main_Blob)
#    set_coverage_flags(hermes_src_main_Bucket)
#    set_coverage_flags(hermes_src_main_Hermes)
endif()
